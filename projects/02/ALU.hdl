// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // Zero and negate x
    OneBitExpander(in=zx, out=zx16);
    OneBitExpander(in=nx, out=nx16);
    Not16(in=zx16, out=notzx16);
    And16(a=x, b=notzx16, out=xand);
    Xor16(a=xand,b=nx16,out=finalx);
    // Zero and negate y
    OneBitExpander(in=zy, out=zy16);
    OneBitExpander(in=ny, out=ny16);
    Not16(in=zy16, out=notzy16);
    And16(a=y, b=notzy16, out=yand);
    Xor16(a=yand,b=ny16,out=finaly);
    //
    Add16(a=finalx, b=finaly, out=addout);
    And16(a=finalx, b=finaly, out=andout);
    OneBitExpander(in=f, out=f16);
    And16(a=addout, b=f16, out=outf1);
    Not16(in=f16, out=notf16);
    And16(a=andout, b=notf16, out=outf2);
    Or16(a=outf1, b=outf2, out=outf3);
    OneBitExpander(in=no, out=no16);
    Xor16(a=outf3, b=no16, out=outtmp);
    // zg and ng
    IsItNegative16(in=outtmp, out=ng);
    Or16Way(in=outtmp, out=notzr);
    Not(in=notzr, out=zr);
    And16(a=outtmp, b=true, out=out);

}
